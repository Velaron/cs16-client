import java.time.LocalDateTime
import java.time.Month
import java.time.temporal.ChronoUnit

apply plugin: 'com.android.application'

android {
    ndkVersion '26.1.10909125'
    namespace 'su.xash.cs16client'

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    defaultConfig {
        applicationId 'su.xash'
        applicationIdSuffix 'cs16client'
        versionName '1.35'
        versionCode getBuildNum()
        minSdkVersion 19
        targetSdk 34
        compileSdk 34
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    externalNativeBuild {
        cmake {
            path file('../CMakeLists.txt')
        }
    }

    lint {
        abortOnError false
    }

    sourceSets {
        main {
            assets {
                srcDirs += "${project.projectDir}/../3rdparty/cs16client-extras"
            }
        }
    }

    androidResources {
        noCompress += ''
    }
}

static def getBuildNum() {
    LocalDateTime now = LocalDateTime.now()
    LocalDateTime releaseDate = LocalDateTime.of(2015, Month.OCTOBER, 29, 0, 0, 0)
    int qBuildNum = releaseDate.until(now, ChronoUnit.DAYS)
    int minuteOfDay = now.getHour() * 60 + now.getMinute()
    return qBuildNum * 10000 + minuteOfDay
}